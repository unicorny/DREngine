cmake_minimum_required(VERSION 3.29.2)

set(DREngine_known_comps static shared)
set(DREngine_comp_static NO)
set(DREngine_comp_shared NO)
foreach (DREngine_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (DREngine_comp IN_LIST DREngine_known_comps)
        set(DREngine_comp_${DREngine_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "DREngine does not recognize component `${DREngine_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (DREngine_comp_static AND DREngine_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "DREngine `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(DREngine_static_targets "${CMAKE_CURRENT_LIST_DIR}/DREngine-static-targets.cmake")
set(DREngine_shared_targets "${CMAKE_CURRENT_LIST_DIR}/DREngine-shared-targets.cmake")

macro(DREngine_load_targets type)
    if (NOT EXISTS "${DREngine_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "DREngine `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${DREngine_${type}_targets}")
endmacro()

if (DREngine_comp_static)
    DREngine_load_targets(static)
elseif (DREngine_comp_shared)
    DREngine_load_targets(shared)
elseif (DEFINED DREngine_SHARED_LIBS AND DREngine_SHARED_LIBS)
    DREngine_load_targets(shared)
elseif (DEFINED DREngine_SHARED_LIBS AND NOT DREngine_SHARED_LIBS)
    DREngine_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${DREngine_shared_targets}")
        DREngine_load_targets(shared)
    else ()
        DREngine_load_targets(static)
    endif ()
else ()
    if (EXISTS "${DREngine_static_targets}")
        DREngine_load_targets(static)
    else ()
        DREngine_load_targets(shared)
    endif ()
endif ()